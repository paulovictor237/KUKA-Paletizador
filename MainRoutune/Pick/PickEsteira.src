&ACCESS RVO1
&REL 58
&PARAM DISKPATH = KRC:\R1\Program\MainRoutune\Pick
DEF PickEsteira ( )
      
   NENHUM = TRUE
   
   ;FOLD PickEsteira de cada receita e esteira
      ;verifica qual opcao de pick do place
      SWITCH PalletAtivo
         CASE 1
            BaseRelativa.X = BaseEsteira1.X + OffsetEsteira1.X
            BaseRelativa.Y = BaseEsteira1.Y + OffsetEsteira1.Y
            BaseRelativa.Z = BaseEsteira1.Z + OffsetEsteira1.Z
            BaseRelativa.A = BaseEsteira1.A + OffsetEsteira1.A
            BaseRelativa.B = BaseEsteira1.B + OffsetEsteira1.B
            BaseRelativa.C = BaseEsteira1.C + OffsetEsteira1.C
            IF (PickOp==1) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_1_P_1
               NENHUM = FALSE
            ENDIF
            IF (PickOp==2) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_1_P_2
               NENHUM = FALSE
            ENDIF
            IF (PickOp==3) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_1_P_3
               NENHUM = FALSE
            ENDIF
            IF (PickOp==4) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_1_P_4
               NENHUM = FALSE
            ENDIF
         CASE 2
            BaseRelativa.X = BaseEsteira2.X + OffsetEsteira2.X
            BaseRelativa.Y = BaseEsteira2.Y + OffsetEsteira2.Y
            BaseRelativa.Z = BaseEsteira2.Z + OffsetEsteira2.Z
            BaseRelativa.A = BaseEsteira2.A + OffsetEsteira2.A
            BaseRelativa.B = BaseEsteira2.B + OffsetEsteira2.B
            BaseRelativa.C = BaseEsteira2.C + OffsetEsteira2.C
            IF (PickOp==1) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_2_P_1
               NENHUM = FALSE
            ENDIF
            IF (PickOp==2) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_2_P_2
               NENHUM = FALSE
            ENDIF
            IF (PickOp==3) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_2_P_3
               NENHUM = FALSE
            ENDIF
            IF (PickOp==4) THEN
               XPICK = StrReceita[ReceitaAtivo].Pick_E_2_P_4
               NENHUM = FALSE
            ENDIF
      ENDSWITCH
      IF (NENHUM==TRUE) THEN
         XPICK = StrReceita[ReceitaAtivo].Pick_E_1_P_1
         BaseRelativa = NULLFRAME
      ENDIF
   ;ENDFOLD

   ;SET BaseRelativa
   $TOOL = TOOL_DATA[1]
   $BASE=BaseRelativa
   
   IF (PalletAtivo==1) THEN
      ;Pega a altura da caixa para fazer as posicoes relativas
      XAPP1PICK = XAppEsteira1
      XAPP2PICK = XAppEsteira1   
   ENDIF
   
   IF (PalletAtivo==2) THEN
      ;Pega a altura da caixa para fazer as posicoes relativas
      XAPP1PICK = XAppEsteira2
      XAPP2PICK = XAppEsteira2   
   ENDIF

   IF (PalletCompleto==TRUE) THEN
      XAPP1PICK.Z = XAPP1PICK.Z + (AlturaPalletCompleto)
   ELSE
      XAPP1PICK.Z = XAPP1PICK.Z + (CamadaAtiva-1)*AlturaCaixa 
   ENDIF

   XAPP2PICK.Z = XPICK.Z + (AlturaCaixa/2)

   ;APROXIMA TOPO
   PTP XAPP1PICK C_DIS   
   
   ;CONFIGURA GARRA
   ConfGarra()
   
   ;APROXIMA DA CAIXA
   LIN XAPP2PICK C_DIS C_DIS
   
   ;PONTO DE PICK
   LIN XPICK
   
   ;Fecha Garra  
   FechaGarra()
   
   ;SOBE EM MOVIMENTO RELATIVO
   XSAIPICK = XPICK
   XSAIPICK.Z=XPICK.Z + (CamadaAtiva)*AlturaCaixa
   LIN XSAIPICK
   
END

DEF PontoReferencia()
   
   
         ;FOLD PTP AppEsteira1 CONT Vel=100 % PDAT3 Tool[1]:GarraUniversal Base[3];%{PE}
      ;FOLD Parameters ;%{h}
         ;Params IlfProvider=kukaroboter.basistech.inlineforms.movement.old; Kuka.IsGlobalPoint=False; Kuka.PointName=AppEsteira3; Kuka.BlendingEnabled=True; Kuka.MoveDataPtpName=PDAT3; Kuka.VelocityPtp=100; Kuka.CurrentCDSetIndex=0; Kuka.MovementParameterFieldEnabled=True; IlfCommand=PTP
      ;ENDFOLD
      $BWDSTART = FALSE
      PDAT_ACT = PPDAT1
      FDAT_ACT = FAppEsteira1
      BAS(#PTP_PARAMS, 100.0)
      SET_CD_PARAMS (0)
      PTP XAppEsteira1 C_Dis
   ;ENDFOLD
   
   
   ;FOLD PTP AppEsteira2 CONT Vel=100 % PDAT2 Tool[1]:GarraUniversal Base[4];%{PE}
      ;FOLD Parameters ;%{h}
         ;Params IlfProvider=kukaroboter.basistech.inlineforms.movement.old; Kuka.IsGlobalPoint=False; Kuka.PointName=AppEsteira2; Kuka.BlendingEnabled=True; Kuka.MoveDataPtpName=PDAT2; Kuka.VelocityPtp=100; Kuka.CurrentCDSetIndex=0; Kuka.MovementParameterFieldEnabled=True; IlfCommand=PTP
      ;ENDFOLD
      $BWDSTART = FALSE
      PDAT_ACT = PPDAT2
      FDAT_ACT = FAppEsteira2
      BAS(#PTP_PARAMS, 100.0)
      SET_CD_PARAMS (0)
      PTP XAppEsteira2 C_Dis
      ;ENDFOLD
      
  
END
