&ACCESS RVP
&REL 34
&PARAM DISKPATH = KRC:\R1\Program\7216_Reckitt_Paletizacao
DEF  MainProgram ()

;FOLD INI;%{PE}
      ;FOLD BASISTECH INI
         GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
         INTERRUPT ON 3 
         BAS (#INITMOV,0 )
      ;ENDFOLD (BASISTECH INI)
      ;FOLD USER INI
         ;Make your modifications here
         $PAL_MODE=TRUE
         TDefault()
         TPallet()
         TMatriz()
         TReceita()
         $TOOL = TOOL_DATA[1]
         $BASE=$NULLFRAME
         $Advance = 3 
         EstadoAutExt = 0
         Estado_Palete1 = 0
         Estado_Palete2 = 0
         UpdateIndex_Est1 = FALSE
         UpdateIndex_Est2 = FALSE
      ;ENDFOLD (USER INI)
;ENDFOLD (INI)
      
;FOLD Sobe Seguro

;Realiza primeiro SAK do programa   
$base=base_data[1]
$tool=tool_data[1]
XSobeSeguro = $POS_ACT
PTP XSobeSeguro
XSobeSeguro.Z = h_SobeSeguro ; TODO: Criar variavel LimiteTopoMaximo
LIN XSobeSeguro

;ENDFOLD
  
;FOLD ABRE_GARRAS
   
;FOLD Gripper SET [1]Gripper1Lado1 State=[1]OPEN Wait 0[s] Check with Strategy 1 ;%{PE}
;FOLD Parameters Parameters ;%{h}
;Params IlfProvider=GripperTech.GripperSet;setgripper=1;setstate=1;setcont=nocont;waittime=0;errorstrategy=1
;ENDFOLD
GRPg_SetStateAndCheck(1, 1, 0, 1)
;ENDFOLD
;FOLD Gripper SET [2]Gripper2Lado1 State=[1]OPEN Wait 0[s] Check with Strategy 1 ;%{PE}
;FOLD Parameters Parameters ;%{h}
;Params IlfProvider=GripperTech.GripperSet;setgripper=2;setstate=1;setcont=nocont;waittime=0;errorstrategy=1
;ENDFOLD
GRPg_SetStateAndCheck(2, 1, 0, 1)
;ENDFOLD
;FOLD Gripper SET [3]Gripper1Lado2 State=[1]OPEN Wait 0[s] Check with Strategy 1 ;%{PE}
;FOLD Parameters Parameters ;%{h}
;Params IlfProvider=GripperTech.GripperSet;setgripper=3;setstate=1;setcont=nocont;waittime=0;errorstrategy=1
;ENDFOLD
GRPg_SetStateAndCheck(3, 1, 0, 1)
;ENDFOLD
;FOLD Gripper SET [4]Gripper2Lado2 State=[1]OPEN Wait 0[s] Check with Strategy 1 ;%{PE}
;FOLD Parameters Parameters ;%{h}
;Params IlfProvider=GripperTech.GripperSet;setgripper=4;setstate=1;setcont=nocont;waittime=0;errorstrategy=1
;ENDFOLD
GRPg_SetStateAndCheck(4, 1, 0, 1)
;ENDFOLD

;ENDFOLD

;FOLD Reseta Paletes
  StrPallet[PalletAtivo].Completo=FALSE
  StrPallet[PalletAtivo].Completo=FALSE
;ENDFOLD

;FOLD SET CAMADA PALLET1
   
  PalletAtivo = Pallet_1
  StrPallet[PalletAtivo].ReceitaAtivo = 1;Mosaico_Est1
  StrPallet[PalletAtivo].CamadaAtiva = 1 ;Camada_P1
  ;StrPallet[PalletAtivo].PlaceAtivo = 4
  StrPallet[PalletAtivo].Livre=TRUE
  AtualizaVariaveis()
   
;ENDFOLD

;FOLD SET CAMADA PALLET2
   
  PalletAtivo = Pallet_2
  StrPallet[PalletAtivo].ReceitaAtivo = 1 ;Mosaico_Est2
  StrPallet[PalletAtivo].CamadaAtiva = 1 ;Camada_P2
  ;StrPallet[PalletAtivo].PlaceAtivo = 4
  StrPallet[PalletAtivo].Livre=TRUE
  AtualizaVariaveis()   
  
;ENDFOLD

;FOLD SET INDEX ESTEIRA 1
  
  IndexacaoVH_Est1 = IndexacaoVH
  NumCaixas_Est1 = NumCaixas 
  
;ENDFOLD

;FOLD SET INDEX ESTEIRA 2
  IndexacaoVH_Est2 = IndexacaoVH
  NumCaixas_Est2 = NumCaixas  
;ENDFOLD

            ;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
      $BWDSTART = FALSE
      PDAT_ACT=PDEFAULT
      FDAT_ACT=FHOME
      BAS (#PTP_PARAMS,100 )
      $H_POS=XHOME
      PTP  XHOME
   ;ENDFOLD

;FOLD CHECK CAMADA 1

IF (NOT Palete1_Completo AND Palete1_OK) or not DryRun_Est1 THEN
   CheckCamadaP1()
ENDIF
;ENDFOLD CHECK CAMADA 1

;FOLD CHECK CAMADA 2
IF (NOT Palete2_Completo AND Palete2_OK) or not DryRun_Est2 THEN
   CheckCamadaP2()
ENDIF
;ENDFOLD CHECK CAMADA 2

;FOLD LOOP Main Routine
   LOOP

;Selecionar Base 1 e TOOL1 e da Touch UP num ponto no meio das duas esteiras
;FOLD PTP AppEsteiras CONT Vel=100 % DEFAULT Tool[1]:GarraUniversal Base[0];%{PE}
;FOLD Parameters ;%{h}
;Params IlfProvider=kukaroboter.basistech.inlineforms.movement.old; Kuka.IsGlobalPoint=False; Kuka.PointName=AppEsteiras; Kuka.BlendingEnabled=True; Kuka.MoveDataPtpName=DEFAULT; Kuka.VelocityPtp=100; Kuka.CurrentCDSetIndex=0; Kuka.MovementParameterFieldEnabled=True; IlfCommand=PTP
;ENDFOLD
$BWDSTART = FALSE
PDAT_ACT = PDEFAULT
FDAT_ACT = FAppEsteiras
BAS(#PTP_PARAMS, 100.0)
SET_CD_PARAMS (0)
PTP XAppEsteiras C_Dis
;ENDFOLD
                       
      Esteira = 2
      if (PickReady_Est1 or DryRun_Est1) and (Esteira==1) and (Palete1_OK or DryRun_Est1) and not StrPallet[Pallet_1].Completo then
         
         Esteira = 2
         
         PalletAtivo = Pallet_1
         ReceitaAtivo = Mosaico_Est1
         StrPallet[PalletAtivo].ReceitaAtivo = ReceitaAtivo
         Retorno_Camada_P1 = StrPallet[PalletAtivo].CamadaAtiva
         
         AtualizaVariaveis()
         PickEsteira()
         
         UpdateIndexCaixas()   
         IndexacaoVH_Est1 = IndexacaoVH
         NumCaixas_Est1 = NumCaixas 
         UpdateIndex_Est1 = TRUE         
         
         PlaceCaixaPallet()
         
         ;wait for UpdateIndex_Est1_ACK
         ;UpdateIndex_Est1 = FALSE
         ;wait for not UpdateIndex_Est1_ACK
            
      else
      if (PickReady_Est2 or DryRun_Est2) and (Esteira==2) and (Palete2_OK or DryRun_Est2) and not StrPallet[Pallet_2].Completo then
         
         Esteira = 1
         
         PalletAtivo = Pallet_2
         ReceitaAtivo = Mosaico_Est2
         StrPallet[PalletAtivo].ReceitaAtivo=ReceitaAtivo
         Retorno_Camada_P2 = StrPallet[PalletAtivo].CamadaAtiva 
         
         AtualizaVariaveis()                   
         PickEsteira()
         
         UpdateIndexCaixas()
         IndexacaoVH_Est2 = IndexacaoVH
         NumCaixas_Est2 = NumCaixas
         UpdateIndex_Est2 = TRUE 
         
         PlaceCaixaPallet()
         
         ;wait for UpdateIndex_Est2_ACK
         ;UpdateIndex_Est2 = FALSE
         ;wait for not UpdateIndex_Est2_ACK
         
      endif
      endif                  
         
      XAppEsteiras.Z = AlturaUltimoDeposito
      
   ENDLOOP
;ENDFOLD
      
END