&ACCESS RVO1
&REL 1
&PARAM DISKPATH = KRC:\R1\Program\7216_Reckitt_Paletizacao\SubRotinas
DEF updateindexcaixas_P2 ( )
      
   ;Captura atual ponto de Place e Camada
   ProxPlace  = StrPallet[Pallet_2].PlaceAtivo
   ProxCamada = StrPallet[Pallet_2].CamadaAtiva

   ;Consome os places que nao ocorre pick   
   FOR count=1 TO StepBuffer_2
      REPEAT
         ;ATUALIZA VARIAVEIS DE SKIP
         ProxPlacesCamada = StrReceita[Mosaico_Est2].PlacesCamada
         ProxLayer        = StrReceitaLayer[Mosaico_Est2,ProxCamada]
         ProxPlaceLayer   = ProxPlace + ProxPlacesCamada*(ProxLayer-1)
         ProxSkipGoHome   = MatrizPontos[Pallet_2,Mosaico_Est2,ProxPlaceLayer].SkipGoHome
         
         ;-> SetCamada()
         ProxPlace = ProxPlace + 1
         ;-> Se a qtd de caixas da camada for acima da qtd do mosaico deve-se subir a camada
         IF ( ProxPlace > StrReceita[Mosaico_Est2].PlacesCamada ) THEN
            ProxPlace = 1 
            ProxCamada = ProxCamada + 1
         ENDIF
         
         ;-> Se a qtd de camadas do pallet for acima da qtd do mosaico
         IF ( ProxCamada > StrReceita[Mosaico_Est2].Camadas ) THEN
            ;Pallet Completo
            ProxPlace = 1
            ProxCamada = 1
            EXIT
         ENDIF
      UNTIL (ProxSkipGoHome == FALSE)
   ENDFOR

   ;Passo para descobrir o endereco do ponto na matriz
   ProxPlacesCamada = StrReceita[Mosaico_Est2].PlacesCamada
   ProxLayer        = StrReceitaLayer[Mosaico_Est2,ProxCamada]
   ProxPlaceLayer   = ProxPlace + ProxPlacesCamada*(ProxLayer-1)
   
   ;Captura o valor da matriz
   IndexacaoVH_Est2 = MatrizPontos[Pallet_2,Mosaico_Est2,ProxPlaceLayer].IndexacaoVH
   NumCaixas_Est2 = MatrizPontos[Pallet_2,Mosaico_Est2,ProxPlaceLayer].NumCaixas 
   
END